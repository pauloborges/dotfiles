# Modified nicoulaj theme

function prompt_borges_precmd {
  vcs_info

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_borges_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_borges_precmd

  # Customizable parameters.
  local max_path_chars=30
  local user_char='❯'
  local root_char='❯❯❯'
  local success_color='%F{071}'
  local failure_color='%F{124}'
  local vcs_info_color='%F{242}'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' unstagedstr '!'
  zstyle ':vcs_info:*' stagedstr '+'
  zstyle ':vcs_info:*' actionformats "%S" "%r/%s/%b %u%c (%a)"
  zstyle ':vcs_info:*' formats "%S" "%r/%s/%b %u%c"
  zstyle ':vcs_info:*' nvcsformats "%~" ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v) '

  # Define prompts.
  PROMPT=""'$python_info[virtualenv]'"%(?.${success_color}.${failure_color})${SSH_TTY:+[%n@%m]}%B%${max_path_chars}<...<"'${vcs_info_msg_0_%%.}'"%<<%(!.${root_char}.${user_char})%b%f "
  RPROMPT="${vcs_info_color}"'${vcs_info_msg_1_}'"%f"
}

prompt_borges_setup "$@"